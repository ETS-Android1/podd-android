apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion "19.1.0"

    defaultConfig {
        applicationId "org.cm.podd.report"
        minSdkVersion 11
        targetSdkVersion 20
        versionCode 32
        versionName "1.0.32"
        buildConfigField "String", "GCM_SERVICE_ID", "\"124985974076\""
        buildConfigField "String", "BUCKET_NAME", "\"podd-dev\""
        buildConfigField "boolean", "REAL_SUBMIT", "true"
    }

    signingConfigs {
        release {
            keyAlias 'podd_report'
            storeFile file('../report.keystore')
            storePassword "xxx"
            keyPassword "yyy"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        server_test {
            buildConfigField "String", "SERVER_URL", "\"http://apidev.cmonehealth.org\""
        }

        server_production {
            buildConfigField "String", "SERVER_URL", "\"https://api.cmonehealth.org\""
            buildConfigField "String", "BUCKET_NAME", "\"podd\""
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('de.keyboardsurfer.android.widget:crouton:1.8.4@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile 'org.mozilla:rhino:1.7R4'
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.google.android.gms:play-services-base:7.8.0'
    compile 'com.google.android.gms:play-services-location:7.8.0'
    compile 'com.google.android.gms:play-services-analytics:7.8.0'
    compile 'com.google.android.gms:play-services-gcm:7.8.0'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'de.hdodenhof:circleimageview:1.2.2'
    compile 'com.squareup.picasso:picasso:2.5.0'
    compile files('libs/aws-android-sdk-2.1.5-core.jar')
    compile files('libs/aws-android-sdk-2.1.5-s3.jar')
    compile files('libs/mpandroidchartlibrary-1-7-4.jar')
    compile 'me.dm7.barcodescanner:zxing:1.8.2'
}

task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("Keystore password: "))
    //def keyPw  = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = storePw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageServer_productionRelease") || theTask.name.equals("packageServer_testRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}